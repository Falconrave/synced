<?xml version="1.0" encoding="utf-8"?>
<!--
  $Id$
-->
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" xmlns="*"
    layout="absolute" styleName="plain"
    verticalScrollPolicy="off" horizontalScrollPolicy="off"
    width="100%" height="100%"
    preloader="com.threerings.msoy.client.Preloader"
    applicationComplete="doInit();">
<mx:Style source="../../../../../../rsrc/css/msoy.css"/>
<mx:Style source="../../../../../../rsrc/css/world-client.css"/>
<mx:Style source="../../../../../../rsrc/css/room-editor.css"/>
<mx:Metadata>
    [ResourceBundle("global")]
    [ResourceBundle("general")]
    [ResourceBundle("chat")]
    [ResourceBundle("game")]
    [ResourceBundle("whirledgame")]
    [ResourceBundle("editing")]
    [ResourceBundle("item")]
    [ResourceBundle("notify")]
    [ResourceBundle("prefs")]
    [ResourceBundle("world")]
    [ResourceBundle("passport")]
    [ResourceBundle("party")]
    [ResourceBundle("studio")]
    [ResourceBundle("homepagegrid")]
    [ResourceBundle("npc")]
</mx:Metadata>
<mx:Script>
    <![CDATA[
        import flash.display.DisplayObject;

        import mx.core.UIComponent;

        import com.threerings.msoy.client.MsoyParameters;
        import com.threerings.msoy.client.UberClient;

        /**
         * Exposed to callers.
         */
        public function loadBytes (bytes :ByteArray) :Boolean
        {
            if (_viewer != null) {
                _viewer.loadBytes(bytes);
                return true;
            }

            return false;
        }

        /**
         * Called by UberClient to set a viewer that's already on-screen.
         */
        public function setViewer (viewer :Object) :void
        {
            _viewer = viewer;
        }

        /**
         * Called by UberClient to set a viewer that must be added to the screen.
         */
        public function setViewerObject (disp :DisplayObject) :void
        {
            if (!(disp is UIComponent)) {
                var wrapper :UIComponent = new UIComponent();
                wrapper.addChild(disp);
                disp = wrapper;
            }
            addChild(disp);

            // also set it as the viewer
            setViewer(disp);
        }

        protected function doInit () :void
        {
            configureUncaughtErrors();

            this.name = "WorldApplication";
            var theThis :Application = this;
            MsoyParameters.init(this, function () :void {
                UberClient.init(theThis);
            });
        }

        /**
         * Configure uncaught error events in a way that's totally flashplayer 9 safe.
         * (Uncaught errors were added in 10.1.53.64)
         */
        protected function configureUncaughtErrors () :void
        {
            var uee :Object = loaderInfo["uncaughtErrorEvents"];
            if (uee == null) {
                return;
            }
            // "uncaughtError" == UncaughtErrorEvent.UNCAUGHT_ERROR
            uee.addEventListener("uncaughtError", function (event :*) :void {
                MsoyClient.log.warning("Uncaught Error in msoy!", event);
            });
        }

        protected var _viewer :Object;
    ]]>
</mx:Script>
</mx:Application>
